buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://repo.grails.org/grails/core" }
        maven { url "https://nexus.k-int.com/content/groups/public/" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsGradlePluginVersion"
        classpath "gradle.plugin.com.github.erdi.webdriver-binaries:webdriver-binaries-gradle-plugin:2.6"
        classpath "org.grails.plugins:hibernate5:7.3.0"
        classpath "com.bertramlabs.plugins:asset-pipeline-gradle:4.3.0"
        classpath 'com.bertramlabs.plugins:sass-dart-asset-pipeline:4.3.0'
        classpath "org.ysb33r.gradle:grolifant50:2.0.0-alpha.6"
    }
}

version "8.0.0-SNAPSHOT"
group "kbplus"

apply plugin:"eclipse"
apply plugin:"idea"
apply plugin:"war"
apply plugin:"org.grails.grails-web"
apply plugin:"com.github.erdi.webdriver-binaries"
apply plugin:"org.grails.grails-gsp"
apply plugin: 'com.bertramlabs.asset-pipeline'
apply plugin:"maven-publish"

repositories {
    mavenCentral()
    maven { url "https://repo.grails.org/grails/core" }
    maven { url "https://nexus.k-int.com/content/groups/public/" }
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

dependencies {
    developmentOnly("org.springframework.boot:spring-boot-devtools")
    compileOnly "io.micronaut:micronaut-inject-groovy"
    console "org.grails:grails-console"
    implementation "org.springframework.boot:spring-boot-starter-logging"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-autoconfigure"
    implementation "org.grails:grails-core"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-tomcat"
    implementation "org.grails:grails-web-boot"
    implementation "org.grails:grails-logging"
    implementation "org.grails:grails-plugin-rest"
    implementation "org.grails:grails-plugin-databinding"
    implementation "org.grails:grails-plugin-i18n"
    implementation "org.grails:grails-plugin-services"
    implementation "org.grails:grails-plugin-url-mappings"
    implementation "org.grails:grails-plugin-interceptors"
    implementation "org.grails.plugins:cache"
    implementation "org.grails.plugins:async"
    implementation "org.grails.plugins:scaffolding"
    implementation "org.grails.plugins:hibernate5"
    implementation "org.hibernate:hibernate-core:5.6.11.Final"
    implementation "org.grails.plugins:events"
    implementation "org.grails.plugins:gsp"
    profile "org.grails.profiles:web"
    runtimeOnly "org.glassfish.web:el-impl:2.2.1-b05"
    runtimeOnly "com.h2database:h2"
    runtimeOnly "org.apache.tomcat:tomcat-jdbc"
    runtimeOnly "org.apache.tomcat:tomcat-jni:9.0.68"
    runtimeOnly "javax.xml.bind:jaxb-api:2.3.1"
    testImplementation "io.micronaut:micronaut-inject-groovy"
    testImplementation "org.grails:grails-gorm-testing-support"
    testImplementation "org.mockito:mockito-core"
    testImplementation "org.grails:grails-web-testing-support"
    testImplementation "org.grails.plugins:geb"
    testImplementation "org.seleniumhq.selenium:selenium-remote-driver:4.0.0"
    testImplementation "org.seleniumhq.selenium:selenium-api:4.0.0"
    testImplementation "org.seleniumhq.selenium:selenium-support:4.0.0"
    testImplementation "io.rest-assured:json-schema-validator:5.3.0"
    testRuntimeOnly "org.seleniumhq.selenium:selenium-chrome-driver:4.0.0"
    testRuntimeOnly "org.seleniumhq.selenium:selenium-firefox-driver:4.0.0"

    
    implementation 'mysql:mysql-connector-java:8.0.31'
    testImplementation 'mysql:mysql-connector-java:8.0.31'

    implementation 'org.elasticsearch:elasticsearch:6.5.0'
    implementation 'org.elasticsearch.client:elasticsearch-rest-high-level-client:6.5.0'
    implementation 'org.apache.poi:poi:3.8'
    implementation 'net.sf.opencsv:opencsv:2.0'
    implementation 'com.googlecode.juniversalchardet:juniversalchardet:1.0.3'
    implementation 'org.apache.commons:commons-exec:1.3'
    implementation 'org.apache.commons:commons-lang3:3.5'
    implementation 'org.apache.httpcomponents:httpcore:4.4.5'
    implementation 'org.apache.httpcomponents:httpmime:4.5.3'
    implementation 'org.apache.httpcomponents:httpclient:4.5.2'
    implementation ( 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1' ) {
      exclude group:"org.codehaus.groovy", module:"groovy"
    }
    implementation 'net.sf.jasperreports:jasperreports:6.20.0'
    implementation 'com.lowagie:itext:2.1.7'
    implementation ('com.k-int:goai:1.0.2') {
      exclude module: 'groovy'
    }
    implementation 'org.grails.plugins:spring-security-core:5.1.1'
    implementation 'com.vladsch.flexmark:flexmark-all:0.64.0'

    implementation 'com.bertramlabs.plugins:sass-dart-asset-pipeline:4.3.0'
    implementation 'com.bertramlabs.plugins:asset-pipeline-grails:4.3.0'
    assets 'com.bertramlabs.plugins:sass-dart-asset-pipeline:4.3.0'
}

war {
    enabled = true
    // archiveName "Wolf.war"
}

bootJar {
  enabled=true
}

bootRun {
    ignoreExitValue true
    jvmArgs(
        '-Dspring.output.ansi.enabled=always', 
        '-noverify', 
        '-XX:TieredStopAtLevel=1',
        '-Xmx1024m')
    sourceResources sourceSets.main
    String springProfilesActive = 'spring.profiles.active'
    systemProperty springProfilesActive, System.getProperty(springProfilesActive)
}

tasks.withType(GroovyCompile) {
    configure(groovyOptions) {
        forkOptions.jvmArgs = ['-Xmx1024m']
    }
}

tasks.withType(Test) {
    useJUnitPlatform()
}


webdriverBinaries {
    if (!System.getenv().containsKey('GITHUB_ACTIONS')) {
        chromedriver {
            version = '2.45.0'
            fallbackTo32Bit = true
        }
        geckodriver '0.30.0'
    }
}

integrationTest {
    systemProperties System.properties
}


tasks.withType(Test) {
    systemProperty "geb.env", System.getProperty('geb.env')
    systemProperty "geb.build.reportsDir", reporting.file("geb/integrationTest")
    // if (!System.getenv().containsKey('GITHUB_ACTIONS')) {
    //     systemProperty 'webdriver.chrome.driver', System.getProperty('webdriver.chrome.driver')
    //     systemProperty 'webdriver.gecko.driver', System.getProperty('webdriver.gecko.driver')
    // } else {
    //     systemProperty 'webdriver.chrome.driver', "${System.getenv('CHROMEWEBDRIVER')}/chromedriver"
    //     systemProperty 'webdriver.gecko.driver', "${System.getenv('GECKOWEBDRIVER')}/geckodriver"
    // }
}

// assets {
//     minifyJs = true
//     minifyCss = true
// }
assets {
  minifyJs = true
  minifyCss = true
  enableSourceMaps = true
  configOptions = [:]
  
  minifyOptions = [
    languageMode: 'ES6',
    targetLanguage: 'ES6', //Can go from ES6 to ES5 for those bleeding edgers
    // optimizationLevel: 'SIMPLE',
    // angularPass: true // Can use @ngInject annotation for Angular Apps
  ]
  
  includes = ['**/*.sass']
  // excludes = ['**/*.sass'] //Example Exclude GLOB pattern

  //for plugin packaging
  packagePlugin=false //set to true if this is a library

  //developmentRuntime can be turned off
  developmentRuntime=true

  //if you want to customize the jar task this task runs on you can specify a jarTaskName
  // jarTaskName=null

  // Can add custom asset locations (directories or individual jar files)
  // from '/vendor/lib'
  // from '/path/to/file.jar'
}

publishing {
   publications {
       maven(MavenPublication) {
           groupId = project.group
           artifactId = 'KBPlusG5'
           version = project.version

           artifact bootJar
           // artifact(tasks.bootJar.get()) 
           // artifactId bootJar.baseName

           pom {
               name = 'KBPlusG5'
               description = 'KBplus'
               url = 'https://github.com/k-int/KBPlusG5'
               scm {
                   connection = 'git@github.com:k-int/KBPlusG5.git'
                   developerConnection = 'git@github.com:k-int/KBPlusG5.git'
                   url = 'https://github.com/k-int/KBPlusG5'
               }
           }
       }
   }
   repositories {
       maven {
            name = "maven-snapshots"
            url = "https://nexus.libsdev.k-int.com/repository/maven-snapshots"
            credentials {
                username = System.getenv("NEXUS_USER")
                password = System.getenv("NEXUS_PASS")
            }
       }
   }
}

